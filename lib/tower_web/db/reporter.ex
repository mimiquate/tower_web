defmodule TowerWeb.DB.Reporter do
  @moduledoc """
  Tower reporter that stores events in a database.
  """
  require Logger

  @behaviour Tower.Reporter

  @impl true
  def report_event(%Tower.Event{} = event) do
    if enabled?() do
      do_report_event(event)
    else
      Logger.debug("[TowerWeb.DB] Reporter disabled, ignoring event")
      :ok
    end
  end

  defp enabled? do
    case Application.fetch_env(:tower_web, :enabled) do
      {:ok, enabled} when is_boolean(enabled) ->
        enabled

      {:ok, other} ->
        raise ArgumentError,
              "expected :tower_web, :enabled to be a boolean, got: #{inspect(other)}"

      :error ->
        true
    end
  end

  defp do_report_event(%Tower.Event{} = event) do
    attrs = %{
      id: event.id,
      similarity_id: event.similarity_id,
      datetime: event.datetime,
      level: event.level,
      kind: event.kind,
      reason: event.reason,
      stacktrace: event.stacktrace,
      metadata: event.metadata,
      request_data: request_data(event.plug_conn)
    }

    case TowerWeb.DB.Events.create_event(attrs) do
      {:error, reason} ->
        Logger.error("[TowerWeb.DB] Error creating event in DB: #{inspect(reason)}")

      {:ok, _event} ->
        nil
    end
  end

  defp request_data(%Plug.Conn{} = conn), do: TowerWeb.DB.RequestData.build(conn)
  defp request_data(_), do: nil
end
